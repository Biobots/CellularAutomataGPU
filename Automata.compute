// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Init
#pragma kernel Process
#pragma kernel Update
#pragma kernel Render

struct Cell
{
    int pre;
    int val;
};

RWStructuredBuffer<Cell> cells;
RWTexture2D<float4> tex;
int scrWidth;
int scrHeight;
float zoom;
int posx;
int posy;
int rule;

float rand(float2 st, float n) {
 	st = floor(st * n);
    return frac(sin(dot(st.xy, float2(12.9898,78.233)))*43758.5453123);
}

[numthreads(320,1,1)]
void Init (uint3 id : SV_DispatchThreadID)
{
    float r = rand(float2(id.x, 11), 10);
    cells[id.x].pre = r < 0.3 ? 1 : 0;
    //cells[id.x].pre = 0;
    cells[id.x].val = cells[id.x].pre;
};

[numthreads(320,1,1)]
void Process (uint3 id : SV_DispatchThreadID)
{
    int val = 0;
    int x = id.x % scrWidth;
    int y = id.x / scrWidth;

    val += x - 1 >= 0 ? cells[id.x - 1].pre : 0;
    val += x + 1 < scrWidth ? cells[id.x + 1].pre : 0;

    if(y - 1 >= 0)
    {
        val += cells[id.x - scrWidth].pre;
        val += x - 1 >= 0 ? cells[id.x - scrWidth - 1].pre : 0;
        val += x + 1 < scrWidth ? cells[id.x - scrWidth + 1].pre : 0;
    }

    if(y + 1 < scrWidth)
    {
        val += cells[id.x + scrWidth].pre;
        val += x - 1 >= 0 ? cells[id.x + scrWidth - 1].pre : 0;
        val += x + 1 < scrWidth ? cells[id.x + scrWidth + 1].pre : 0;
    }

    int offset = cells[id.x].pre == 0 ? 9 : 0;
    cells[id.x].val = ((rule >> offset) >> val) & 1;
};

[numthreads(320,1,1)]
void Update (uint3 id : SV_DispatchThreadID)
{
    cells[id.x].pre = cells[id.x].val;
}

float4 GetVal(uint2 coord)
{
    if(cells[coord.y*scrWidth+coord.x].val==1)
    {
        return float4(0.0, 0.0, 0.0, 0.0);
    }
    else
    {
        return float4(1.0, 1.0, 1.0, 0.0);
    }
}

[numthreads(4,4,1)]
void Render (uint3 id : SV_DispatchThreadID)
{
    float x = id.x / zoom + posx;
    float y = id.y / zoom + posy;
    tex[id.xy] = GetVal(uint2(x, y));
}